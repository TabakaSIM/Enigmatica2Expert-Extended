#priority 100
#modloaded thaumcraft

import native.com.blamejared.compat.thaumcraft.handlers.ThaumCraft;
import native.net.minecraft.entity.Entity;
import native.net.minecraft.entity.passive.AbstractHorse;
import native.net.minecraft.entity.passive.EntityAnimal;
import native.net.minecraft.entity.passive.EntityOcelot;
import native.net.minecraft.entity.passive.EntityTameable;
import native.net.minecraft.entity.player.EntityPlayer;
import native.net.minecraft.util.EnumParticleTypes;
import native.net.minecraft.util.math.RayTraceResult;
import native.net.minecraft.world.World;
import native.net.minecraft.world.WorldServer;
import native.net.minecraftforge.fml.common.network.NetworkRegistry;
import native.thaumcraft.api.aspects.Aspect;
import native.thaumcraft.api.casters.FocusEffect;
import native.thaumcraft.api.casters.NodeSetting;
import native.thaumcraft.api.casters.Trajectory;
import native.thaumcraft.client.fx.ParticleEngine;
import native.thaumcraft.client.fx.particles.FXGeneric;
import native.thaumcraft.common.lib.network.PacketHandler;
import native.thaumcraft.common.lib.network.fx.PacketFXFocusPartImpact;
import native.forestry.api.lepidopterology.IEntityButterfly;
import native.net.minecraft.util.SoundCategory;
import native.net.minecraft.init.SoundEvents;

zenClass SpellButtercatch extends FocusEffect {

    zenConstructor() {
        super();
    }

    //======================
    //Set up some basic info
    //======================

    function getResearch() as string {
        return 'BUTTERCATCH';
    }
    
    function getKey() as string {
        return 'thaumcraft.BUTTERCATCH';
    }

    //===================================
    //Set up focalmanipulator spell stats
    //===================================
    
    function getAspect() as Aspect {
        return ThaumCraft.getAspect(Aspects('〇')[0]);
    }

    function getComplexity() as int {
        return this.getSettingValue('range') / 5;
    }

    function createSettings() as NodeSetting[] {
        return [
            NodeSetting('range',     'focus.common.range',      NodeSetting.NodeSettingIntList([25, 75, 125, 250], ['25', '75', '125', '250']))
        ];
    }

    //==========================
    //Set up executable function
    //==========================

    function execute(target as RayTraceResult, trajectory as Trajectory, finalPower as float, num as int) as bool {
        val world = this.getPackage().world;
        val pos = target.getBlockPos();
        val range = this.getSettingValue('range');
        PacketHandler.INSTANCE.sendToAllAround(PacketFXFocusPartImpact(target.hitVec.x, target.hitVec.y, target.hitVec.z, [getKey()]), NetworkRegistry.TargetPoint(world.provider.getDimension(), target.hitVec.x, target.hitVec.y, target.hitVec.z, 64.0));

        var didSomething = false;
        for entity in world.wrapper.getEntities() {
            if (entity.native instanceof IEntityButterfly && entity.native.getDistanceSq(pos) < 250) {
                entity.position = pos.wrapper;
                didSomething = true;
            }
        }
        if (didSomething) world.playSound(null, pos, SoundEvents.ENTITY_ENDERMEN_TELEPORT, SoundCategory.AMBIENT, 1.0f, world.rand.nextFloat() * 0.4f + 0.8f);

        return didSomething;
    }

    function onCast(caster as Entity) {
        
    }

    //==================
    //Particle rendering
    //==================

    function renderParticleFX(world as World, posX as double, posY as double, posZ as double, motionX as double, motionY as double, motionZ as double) as void {
        val fb as FXGeneric = FXGeneric(world, posX, posY, posZ, motionX, motionY, motionZ);
    
        fb.setMaxAge(8); //Particle lifetime (in ticks)
        fb.setRBGColorF(0.784f + (world.rand.nextFloat() - 0.5f) * 0.05f, 0.784f + (world.rand.nextFloat() - 0.5f) * 0.05f, 0.784f + (world.rand.nextFloat() - 0.5f) * 0.05f);
        //fb.setAlphaF(0.0f); //Transparency
        fb.setGridSize(16); //Particle texture size
        fb.setParticles(72 + world.rand.nextInt(4), 1, 1);
        fb.setScale(2.0 + world.rand.nextFloat() * 4.0); //Particle size
        fb.setLoop(false);
        fb.setSlowDown(0.9);
        fb.setGravity(0.0);
        fb.setRotationSpeed(world.rand.nextFloat(), 0.0);
    
        ParticleEngine.addEffectWithDelay(world, fb, world.rand.nextInt(4));
    }

}
